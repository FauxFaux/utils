#!/usr/bin/perl -w

#===============================================================
# $Id: afv_move,v 1.2 2004/04/12 20:16:05 sunny Exp $
# Reads file names generated by afv(1) and stores them into
# subdirectories of the format yyyy-mm-dd/hh/FILE
#
# Character set: UTF-8
# License: GNU General Public License
# ©opyleft 2004 Øyvind A. Holm <sunny@sunbase.org>
#===============================================================

use strict;
use File::Copy;
use File::Path;
use Getopt::Std;
our ($opt_c, $opt_d, $opt_h, $opt_o, $opt_s, $opt_v) =
    (     0,    ".",      0,      0,      0,      0);
getopts('cd:hosv');

$| = 1;

if ($opt_h) {
	print(<<END);

Syntax: $0 [options] [file_with_filenames [...]]

The program reads file names from stdin or files on the command line.
These files will be placed in a subdirectory structure like

yyyy-mm-dd/hh/FILE

Options:

-c    Copy files instead of move
-d X  Place files under directory X (Default: "$opt_d")
-h    Help me, please.
-o    Overwrite existing files
-s    Simulate, don't really move files
-v    Verbose execution

Note: Files on the command line will not be moved themselves, but shall
contain file names of the relevant files created by afv(1).

Examples:

ls | $0
find /var/tmp/afvroot | $0

END
	exit(0);
}

my $simul_str = $opt_s ? " (simulating)" : "";

while (<>) {
	chomp();
	if (/^(.*)\/([^\/]+?)$/) {
		my ($Path, $File) =
		   (   $1,    $2);
		if ($File =~ /^(.*?)\b(\d\d\d\d)(\d\d)(\d\d)T(\d\d)(\d\d)(\d\d)Z(\..+)/) {
			my ($Pre, $Year, $Mon, $Day, $Hour, $Min, $Sec, $Rest) =
			   (  $1,    $2,   $3,   $4,    $5,   $6,   $7,    $8);
			my $From = "$Path/$File";
			if (-e $From) {
				if (-f $From) {
					my $Dir = "$opt_d/$Year/$Mon/$Day/$Hour";

					if (!$opt_o && -e "$Dir/$File") {
						warn("$Dir/$File: File already exists, will not overwrite\n");
					} else {
						$opt_s || -d $Dir || mkpath($Dir, $opt_v ? 1 : 0, 0777) || die("mkpath(\"$Dir\", 0, 0777): $!");
						if ($opt_c) {
							$opt_v && print("Copying \"$From\" to \"$Dir/\"$simul_str...");
							$opt_s || copy($From, $Dir) || die("copy(\"$From\", \"$Dir\"): $!");
							$opt_v && print("OK\n");
						} else {
							$opt_v && print("Moving \"$From\" to \"$Dir/\"$simul_str...");
							$opt_s || move($From, $Dir) || die("move(\"$From\", \"$Dir\"): $!");
							$opt_v && print("OK\n");
						}
					}
				} else {
					warn("Ignoring non-regular file $From\n");
				}
			} else {
				warn("$From: File not found\n");
			}
		}
	}
}

__END__

# vim: set fileencoding=UTF-8 filetype=perl foldmethod=marker foldlevel=0 :
# End of file $Id: afv_move,v 1.2 2004/04/12 20:16:05 sunny Exp $
