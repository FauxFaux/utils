#!/usr/bin/perl -w

#=======================================================================
# $Id$
# Encode videos for Nokia N95.
#
# Character set: UTF-8
# ©opyleft 2008– Øyvind A. Holm <sunny@sunbase.org>
# License: GNU General Public License version 2 or later, see end of 
# file for legal stuff.
#=======================================================================

BEGIN {
    our @version_array;
}

use strict;
use Getopt::Long;

$| = 1;

our $Debug = 0;

our %Std = (
    'encoder' => "ffmpeg",
);

our %Opt = (
    'debug' => 0,
    'encoder' => $Std{'encoder'},
    'force' => 0,
    'help' => 0,
    'verbose' => 0,
    'version' => 0,
);

our $progname = $0;
$progname =~ s/^.*\/(.*?)$/$1/;

my $rcs_id = '$Id$';
my $id_date = $rcs_id;
$id_date =~ s/^.*?\d+ (\d\d\d\d-.*?\d\d:\d\d:\d\d\S+).*/$1/;

push(@main::version_array, $rcs_id);

Getopt::Long::Configure("bundling");
GetOptions(
    "debug" => \$Opt{'debug'},
    "encoder|e=s" => \$Opt{'encoder'},
    "force|f" => \$Opt{'force'},
    "help|h" => \$Opt{'help'},
    "verbose|v+" => \$Opt{'verbose'},
    "version" => \$Opt{'version'},
) || die("$progname: Option error. Use -h for help.\n");

$Opt{'debug'} && ($Debug = 1);
$Opt{'help'} && usage(0);
$Opt{'version'} && print_version();

if ($#ARGV >= 0) {
    for (@ARGV) {
        process_file($_);
    }
} else {
    process_file("-");
}

sub process_file {
    # {{{
    my $Filename = shift;
    D("process_file('$Filename')");
    my $use_stdin = ($Filename eq "-") ? 1 : 0;
    if ($use_stdin || -e $Filename) {
        if (-e "$Filename.mp4" && !$Opt{'force'}) {
            warn("$progname: $Filename.mp4: Will not overwrite existing file. Use --force (-f) option to override.\n");
            return;
        }
        msg(1, "Encoding $Filename...");
        if ($Opt{'encoder'} eq "mencoder") {
            # Recipe created by Tomasz Sterna, found at 
            # <http://notes.xiaoka.com/2008/03/29/encode-mp4-files-for-nokia-n95-with-mencoder/>.
            # Has sound sync issues.
            mysyst(
                "mencoder",
                "-of", "lavf",
                "-lavfopts", "format=mp4",
                "-oac", "lavc",
                "-ovc", "lavc",
                "-lavcopts", "aglobal=1:vglobal=1:acodec=libfaac:abitrate=128:vcodec=mpeg4:keyint=25",
                "-ofps", "25",
                "-af", "lavcresample=44100",
                "-vf", "harddup,scale=320:-3",
                "-mc", "0",
                "-noskip",
                $Filename,
                "-o",
                $use_stdin ? "-" : "$Filename.mp4"
            );
        } elsif ($Opt{'encoder'} eq "ffmpeg") {
            # Modified version of something found at 
            # <http://www.n95users.com/forum/general-95/2602-howto-convert-avi-nokia-n95-compatible-avi-embedded-subtitle-linux.html>. 
            # Does not keep aspect ratio, fills the entire N95 screen, 
            # but that’s probably better than using 30% of the screen in 
            # extreme cases.
            mysyst(
                "ffmpeg",
                "-i", $Filename,    # Input filename
                "-f", "mp4",        # Format
                "-vcodec", "mpeg4", # Video codec
                "-b", "250000",     # Video bitrate
                "-r", "25",         # Frames per second
                "-s", "320x240",    # Frame size
                "-acodec", "aac",   # Audio codec
                "-ar", "44100",     # Audio sampling frequency
                "-ab", "64",        # Audio bitrate in kbit/s
                "-ac", "2",         # Number of audio channels
                "-me", "epzs",      # Motion estimation method (best quality)
                "-y",               # Overwrite output files
                $use_stdin          # Output filename
                    ? "-"
                    : "$Filename.mp4"
            );
        } else {
            die("$progname: $Opt{'encoder'}: Unknown --encoder value.\n");
        }
    } else {
        warn("$progname: $Filename: File not found");
    }
    return;
    # }}}
} # process_file()

sub mysyst {
    # Customised system() {{{
    my @Args = @_;
    my $system_txt = sprintf("system(\"%s\");", join("\", \"", @Args));
    msg(2, "Executing \"" . join(" ", @_) . "\"...");
    system(@_);
    # }}}
} # mysyst()

sub print_version {
    # Print program version {{{
    for (@main::version_array) {
        print("$_\n");
    }
    exit(0);
    # }}}
} # print_version()

sub usage {
    # Send the help message to stdout {{{
    my $Retval = shift;

    print(<<END);

$rcs_id

Usage: $progname [options] [file [files [...]]]

Encode movies for the Nokia N95. If no files are specified, the program 
encodes from stdin to stdout.

Options:

  -e x, --encoder x
    Use encoder x:
      ffmpeg
      mencoder (may have audio/video sync problems)
    Default: $Std{'encoder'}
  -h, --help
    Show this help.
  -f, --force
    Overwrite existing files (input filename with .mp4 extension).
  -v, --verbose
    Increase level of verbosity. Can be repeated.
  --version
    Print version information.
  --debug
    Print debugging messages.

END
    exit($Retval);
    # }}}
} # usage()

sub msg {
    # Print a status message to stderr based on verbosity level {{{
    my ($verbose_level, $Txt) = @_;

    if ($Opt{'verbose'} >= $verbose_level) {
        print(STDERR "$progname: $Txt\n");
    }
    # }}}
} # msg()

sub D {
    # Print a debugging message {{{
    $Debug || return;
    my @call_info = caller;
    chomp(my $Txt = shift);
    my $File = $call_info[1];
    $File =~ s#\\#/#g;
    $File =~ s#^.*/(.*?)$#$1#;
    print(STDERR "$File:$call_info[2] $$ $Txt\n");
    return("");
    # }}}
} # D()

__END__

# Plain Old Documentation (POD) {{{

=pod

=head1 NAME



=head1 REVISION

$Id$

=head1 SYNOPSIS

 [options] [file [files [...]]]

=head1 DESCRIPTION



=head1 OPTIONS

=over 4

=item B<-h>, B<--help>

Print a brief help summary.

=item B<-v>, B<--verbose>

Increase level of verbosity. Can be repeated.

=item B<--version>

Print version information.

=item B<--debug>

Print debugging messages.

=back

=head1 BUGS



=head1 AUTHOR

Made by Øyvind A. Holm S<E<lt>sunny@sunbase.orgE<gt>>.

=head1 COPYRIGHT

Copyleft © Øyvind A. Holm E<lt>sunny@sunbase.orgE<gt>
This is free software; see the file F<COPYING> for legalese stuff.

=head1 LICENCE

This program is free software; you can redistribute it and/or modify it 
under the terms of the GNU General Public License as published by the 
Free Software Foundation; either version 2 of the License, or (at your 
option) any later version.

This program is distributed in the hope that it will be useful, but 
WITHOUT ANY WARRANTY; without even the implied warranty of 
MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.
See the GNU General Public License for more details.

You should have received a copy of the GNU General Public License along 
with this program; if not, write to the Free Software Foundation, Inc., 
59 Temple Place, Suite 330, Boston, MA  02111-1307  USA

=head1 SEE ALSO

=cut

# }}}

# vim: set fenc=UTF-8 ft=perl fdm=marker ts=4 sw=4 sts=4 et fo+=w :
# End of file $Id$
