#!/usr/bin/perl -w

#=============================================================================
# $Id: h2u,v 1.1 2001/07/26 18:44:36 sunny Exp $
# Converts from numeric entities in HTML/SGML (&#9786; and &#x263a;) to UTF-8.
# If "-l" is specified as first parameter, also convert latin-1 characters.
# Created by &#216;yvind A. Holm. License: GNU GPL.
#=============================================================================

use strict;

my $conv_latin = 0;
defined($ARGV[0]) && ($ARGV[0] =~ /-l/) && ($conv_latin = 1, shift);

while (<>) {
	$conv_latin && s/([\x80-\xff])/widechar(ord($1))/ge;
	s/&#(\d{1,7});/widechar($1)/ge;
	s/&#x([0-9a-f]{1,6});/widechar(hex($1))/gei;
	print;
}

sub widechar {
	my $Val = shift;
	if ($Val < 0x80) {
		return sprintf("%c", $Val);
	} elsif ($Val < 0x800) {
		return sprintf("%c%c", 0xC0 | ($Val >> 6),
		                       0x80 | ($Val & 0x3F));
	} elsif ($Val < 0x10000) {
		return sprintf("%c%c%c", 0xE0 | ($Val>>12),
		                         0x80 | (($Val>>6) & 0x3F),
		                         0x80 | ($Val & 0x3F));
	} elsif ($Val < 0x200000) {
		return sprintf("%c%c%c%c", 0xF0 | ($Val>>18),
		                           0x80 | (($Val>>12) & 0x3F),
		                           0x80 | (($Val>>6) & 0x3F),
		                           0x80 | ($Val & 0x3F));
	}
} # widechar()

__END__
